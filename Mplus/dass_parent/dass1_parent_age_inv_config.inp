Title: Parent DASS Age Measurement Invariance Configural Model;

Data:
   File is dass_parent_1.txt;

Variable:
   Names are studentid treatment female age_w dass1_1 dass2_1 dass3_1
   dass4_1 dass5_1 dass6_1 dass7_1 half; 
   Missing are . ;

  Usevariable are
	dass1_1 dass2_1 dass3_1 dass4_1 dass5_1 dass6_1 dass7_1 age_c;
   
  Categorical is
	dass1_1 dass2_1 dass3_1 dass4_1 dass5_1 dass6_1 dass7_1;
    
  Group is age_c (1 = Older 0 = Younger);

Define: 
	age_c = 0;
	if(age_w ge 40) then age_c = 1;

Analysis:
   Estimator = wlsmv;
   Parameterization = theta;
  
  
!!! Model for Younger Parents !!!

Model:
  ! Factor loadings all freely estimated
  dassp_1 by
    dass1_1-dass7_1* (L1-L7);
    
  dass1_1 with dass2_1;
  dass6_1 with dass7_1;
  
  !estimating thresholds freely
  [dass1_1$1-dass7_1$1*] (T1_1-T1_7);
  [dass1_1$2-dass7_1$2*] (T2_1-T2_7);
  [dass1_1$3-dass7_1$3*] (T3_1-T3_7);
  
  !constraining item residual variance to 1 for identification
  dass1_1-dass7_1@1;

  !fixing latent mean to zero for identification
  [dassp_1@0];
  
  !fixing latent variance to one for identification
  dassp_1@1;
  
!!! Model for Older Parents !!!

Model Older:
  ! Factor loadings all freely estimated
  dassp_1 by dass1_1-dass7_1*;
    
  dass1_1 with dass2_1;
  dass6_1 with dass7_1;
  
  ! estimating thresholds freely
  [dass1_1$1-dass7_1$1*];
  [dass1_1$2-dass7_1$2*];
  [dass1_1$3-dass7_1$3*];
  
  ! constraining item residual variance to 1 for identification
  dass1_1-dass7_1@1;

  ! fixing latent factor mean to zero for identification
  [dassp_1@0];
  
  ! fixing latent factor variance to one for identification
  dassp_1@1;
     
Output: sampstat stdyx modindices(all);
Savedata: Difftest is dass1_parent_age_inv_config.dat;
