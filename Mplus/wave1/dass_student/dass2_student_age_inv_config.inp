Title: Student DASS Age Measurement Invariance Configural Model;

Data:
   File is ../dat_stu.txt;

Variable:
   Names are studentid index treatment girl age_w child_grade in_ukr
   guardian_living guardian_region 
   grit1_1 grit2_1 grit3_1 grit4_1 grit5_1 grit6_1 grit7_1 grit8_1 
   loc1_1 loc2_1 loc3_1 loc4_1 
   dass1_1 dass2_1 dass3_1 dass4_1 dass5_1 dass6_1 dass7_1 dass8_1 
   dass9_1 dass10_1 dass11_1 dass12_1 dass13_1 dass14_1 
   sleep1_1 sleep2_1 sleep3_1 sleep4_1 sleep5_1 sleep6_1 sleep7_1 
   efficacy_1 edugoals_1 remotediff_1 timeclass_1 timehomework_1 
   grit1_2 grit2_2 grit3_2 grit4_2 grit5_2 grit6_2 grit7_2 grit8_2 
   loc1_2 loc2_2 loc3_2 loc4_2 
   dass1_2 dass2_2 dass3_2 dass4_2 dass5_2 dass6_2 dass7_2 dass8_2 
   dass9_2 dass10_2 dass11_2 dass12_2 dass13_2 dass14_2 
   efficacy_2 edugoals_2 timeclass_2 timehomework_2 compmath_2 half; 
   Missing are . ;

  Usevariable are
	dass1_2 dass2_2 dass3_2 dass4_2 dass5_2 dass6_2 dass7_2 dass8_2 
  dass9_2 dass10_2 dass11_2 dass12_2 dass13_2 dass14_2 age_c;
   
  Categorical is
	dass1_2 dass2_2 dass3_2 dass4_2 dass5_2 dass6_2 dass7_2 dass8_2 
  dass9_2 dass10_2 dass11_2 dass12_2 dass13_2 dass14_2;
  
  Idvariable is studentid;  
    
  Group is age_c (1 = Older 0 = Younger);

Define: 
	age_c = 0;
	if(age_w ge 13) then age_c = 1;

Analysis:
   Estimator = wlsmv;
   Parameterization = theta;
   
  
!!! Model for Younger Students !!!

Model:
  ! Factor loadings all freely estimated
  stress_2 by
    dass1_2* dass4_2* dass6_2* dass8_2* dass9_2* dass10_2* dass12_2* (L1-L7);
    
  anxiety_2 by
    dass2_2* dass3_2* dass5_2* dass7_2* dass11_2* dass13_2* dass14_2* (L8-L14);  
  
  !estimating thresholds freely
  [dass1_2$1-dass14_2$1*] (T1_1-T1_14);
  [dass1_2$2-dass14_2$2*] (T2_1-T2_14);
  [dass1_2$3-dass14_2$3*] (T3_1-T3_14);
  
  !constraining item residual variance to 1 for identification
  dass1_2-dass14_2@1;

  !fixing latent mean to zero for identification
  [stress_2@0];
  [anxiety_2@0];
  
  !fixing latent variance to one for identification
  stress_2@1;
  anxiety_2@1;
  
!!! Model for Older Students !!!

Model Older:
  ! Factor loadings all freely estimated
  stress_2 by
    dass1_2* dass4_2* dass6_2* dass8_2* dass9_2* dass10_2* dass12_2*;
    
  anxiety_2 by
    dass2_2* dass3_2* dass5_2* dass7_2* dass11_2* dass13_2* dass14_2*;
  
  ! estimating thresholds freely
  [dass1_2$1-dass14_2$1*];
  [dass1_2$2-dass14_2$2*];
  [dass1_2$3-dass14_2$3*];
  
  ! constraining item residual variance to 1 for identification
  dass1_2-dass14_2@1;

  ! fixing latent factor mean to zero for identification
  [stress_2@0];
  [anxiety_2@0];
  
  ! fixing latent factor variance to one for identification
  stress_2@1;
  anxiety_2@1;
     
Output: sampstat stdyx modindices(all);
Savedata: Difftest is dass2_student_age_inv_config.dat;
