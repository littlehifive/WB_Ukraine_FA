Title: Student Grit Age Measurement Invariance Scalar Model;

Data:
   File is ../dat_stu.txt;

Variable:
   Names are studentid index treatment girl age_w child_grade in_ukr
   guardian_living guardian_region 
   grit1_1 grit2_1 grit3_1 grit4_1 grit5_1 grit6_1 grit7_1 grit8_1 
   loc1_1 loc2_1 loc3_1 loc4_1 
   dass1_1 dass2_1 dass3_1 dass4_1 dass5_1 dass6_1 dass7_1 dass8_1 
   dass9_1 dass10_1 dass11_1 dass12_1 dass13_1 dass14_1 
   sleep1_1 sleep2_1 sleep3_1 sleep4_1 sleep5_1 sleep6_1 sleep7_1 
   efficacy_1 edugoals_1 remotediff_1 timeclass_1 timehomework_1 
   grit1_2 grit2_2 grit3_2 grit4_2 grit5_2 grit6_2 grit7_2 grit8_2 
   loc1_2 loc2_2 loc3_2 loc4_2 
   dass1_2 dass2_2 dass3_2 dass4_2 dass5_2 dass6_2 dass7_2 dass8_2 
   dass9_2 dass10_2 dass11_2 dass12_2 dass13_2 dass14_2 
   efficacy_2 edugoals_2 timeclass_2 timehomework_2 compmath_2 half; 
   Missing are . ;

  Usevariable are
	grit1_2 grit5_2 grit6_2 grit7_2 grit8_2 age_c;
   
  Categorical is
	grit1_2 grit5_2 grit6_2 grit7_2 grit8_2;
   
  Idvariable is studentid;  
   
  Group is age_c (1 = Older 0 = Younger);

Define: 
	age_c = 0;
	if(age_w ge 13) then age_c = 1;

Analysis:
   Estimator = wlsmv;
   Parameterization = theta;
   Difftest = grit2_student_age_inv_metric.dat
  
  
!!! Model for Younger Students !!!

Model:
! Factor loadings all freely estimated
  grit_2 by
    grit1_2* grit5_2* grit6_2* grit7_2* grit8_2* (L1-L5);
    
  grit1_2 with grit6_2; !worded positively
  grit1_2 with grit7_2; !both about schoolwork
  
  !estimating thresholds freely
  [grit1_2$1* grit5_2$1* grit6_2$1* grit7_2$1* grit8_2$1*] (T1_1 T5_1 T6_1 T7_1 T8_1);
  [grit1_2$2* grit5_2$2* grit6_2$2* grit7_2$2* grit8_2$2*] (T1_2 T5_2 T6_2 T7_2 T8_2);
  [grit1_2$3* grit5_2$3* grit6_2$3* grit7_2$3* grit8_2$3*] (T1_3 T5_3 T6_3 T7_3 T8_3);
  [grit1_2$4* grit5_2$4* grit6_2$4* grit7_2$4* grit8_2$4*] (T1_4 T5_4 T6_4 T7_4 T8_4);
  
  !constraining item residual variance to 1 for identification
  grit1_2@1;
  grit5_2@1; 
  grit6_2@1;
  grit7_2@1;
  grit8_2@1;

  !fixing latent mean to zero for identification
  [grit_2@0];
  
  !fixing latent variance to one for identification
  grit_2@1;
  
!!! Model for Older Students !!!

Model Older:
  ! Factor loadings constrained to *be equal to the control group*
  grit_2 by
    grit1_2* grit5_2* grit6_2* grit7_2* grit8_2* (L1-L5);
    
  grit1_2 with grit6_2; !worded positively
  grit1_2 with grit7_2; !both about schoolwork
  
  ! *thresholds held equal to the control group when left blank*
  
  ! constraining item residual variance to 1 for identification
  grit1_2@1;
  grit5_2@1; 
  grit6_2@1;
  grit7_2@1;
  grit8_2@1;

  ! latent factor mean *estimated*
  [grit_2*];
  
  ! latent factor variance *estimated*
  grit_2*;
     
Output: sampstat stdyx modindices(all);
Savedata: Difftest is grit2_student_age_inv_scalar.dat;
