Title: Student Grit Gender Measurement Invariance Configural Model;

Data:
   File is ../dat_stu_1.txt;

Variable:
   Names are studentid index treatment girl age_w child_grade in_ukr
   guardian_living guardian_region 
   grit1_1 grit2_1 grit3_1 grit4_1 grit5_1 grit6_1 grit7_1 grit8_1 
   loc1_1 loc2_1 loc3_1 loc4_1 
   dass1_1 dass2_1 dass3_1 dass4_1 dass5_1 dass6_1 dass7_1 dass8_1 
   dass9_1 dass10_1 dass11_1 dass12_1 dass13_1 dass14_1 
   sleep1_1 sleep2_1 sleep3_1 sleep4_1 sleep5_1 sleep6_1 sleep7_1 
   edugoals_1 half; 
   Missing are . ;

  Usevariable are
	grit1_1 grit5_1 grit6_1 grit7_1 grit8_1;
   
  Categorical is
	grit1_1 grit5_1 grit6_1 grit7_1 grit8_1;
  
  Idvariable is studentid;  
    
  Group is girl (1 = Female 0 = Male);

Analysis:
   Estimator = wlsmv;
   Parameterization = theta;
   
  
!!! Model for Male !!!

Model:
  ! Factor loadings all freely estimated
  grit_1 by
    grit1_1* grit5_1* grit6_1* grit7_1* grit8_1* (L1-L5);
    
  grit1_1 with grit6_1; !worded positively
  grit1_1 with grit7_1; !both about schoolwork
  
  !estimating thresholds freely
  [grit1_1$1* grit5_1$1* grit6_1$1* grit7_1$1* grit8_1$1*] (T1_1 T5_1 T6_1 T7_1 T8_1);
  [grit1_1$2* grit5_1$2* grit6_1$2* grit7_1$2* grit8_1$2*] (T1_2 T5_2 T6_2 T7_2 T8_2);
  [grit1_1$3* grit5_1$3* grit6_1$3* grit7_1$3* grit8_1$3*] (T1_3 T5_3 T6_3 T7_3 T8_3);
  [grit1_1$4* grit5_1$4* grit6_1$4* grit7_1$4* grit8_1$4*] (T1_4 T5_4 T6_4 T7_4 T8_4);
  
  !constraining item residual variance to 1 for identification
  grit1_1@1;
  grit5_1@1; 
  grit6_1@1;
  grit7_1@1;
  grit8_1@1;

  !fixing latent mean to zero for identification
  [grit_1@0];
  
  !fixing latent variance to one for identification
  grit_1@1;
  
!!! Model for Female !!!

Model Female:
  ! Factor loadings all freely estimated
  grit_1 by
    grit1_1* grit5_1* grit6_1* grit7_1* grit8_1*;
    
  grit1_1 with grit6_1; !worded positively
  grit1_1 with grit7_1; !both about schoolwork
  
  !estimating thresholds freely
  [grit1_1$1* grit5_1$1* grit6_1$1* grit7_1$1* grit8_1$1*];
  [grit1_1$2* grit5_1$2* grit6_1$2* grit7_1$2* grit8_1$2*];
  [grit1_1$3* grit5_1$3* grit6_1$3* grit7_1$3* grit8_1$3*];
  [grit1_1$4* grit5_1$4* grit6_1$4* grit7_1$4* grit8_1$4*];
  
  !constraining item residual variance to 1 for identification
  grit1_1@1;
  grit5_1@1; 
  grit6_1@1;
  grit7_1@1;
  grit8_1@1;

  !fixing latent mean to zero for identification
  [grit_1@0];
  
  !fixing latent variance to one for identification
  grit_1@1;
     
Output: sampstat stdyx modindices(all);
Savedata: Difftest is grit1_student_gender_inv_config.dat;
