Title: Student Grit Age Measurement Invariance Scalar Model;

Data:
   File is ../dat_stu_2.txt;

Variable:
   Names are studentid girl age_w grito1_2 grito5_2 grito6_2 
   grito7_2 grito8_2 grit1_2 grit2_2 grit3_2 grit4_2 grit5_2 
   grit6_2 grit7_2 grit8_2 loc1_2 loc2_2 loc3_2 loc4_2 dass1_2 
   dass2_2 dass3_2 dass4_2 dass5_2 dass6_2 dass7_2 dass8_2 
   dass9_2 dass10_2 dass11_2 dass12_2 dass13_2 dass14_2 
   dass15_2 dass16_2 dass17_2 dass18_2 dass19_2 dass20_2 dass21_2 
   treatment half;
   Missing are . ;

  Usevariable are
	grit1_2 grit2_2 grit3_2 grit4_2 grit5_2 grit6_2 grit7_2 grit8_2 age_c;
   
  Categorical is
	grit1_2 grit2_2 grit3_2 grit4_2 grit5_2 grit6_2 grit7_2 grit8_2;
  
  Idvariable is studentid;  
  
  Group is age_c (1 = Older 0 = Younger);

Define: 
	age_c = 0;
	if(age_w ge 13) then age_c = 1;

Analysis:
   Estimator = wlsmv;
   Parameterization = theta;
   Difftest = grit2_student_age_inv_metric.dat
  
  
!!! Model for Younger Students !!!

Model:
  ! Factor loadings all freely estimated
  pers_2 by
    grit2_2* grit4_2* grit7_2* grit8_2* (L1-L4);
    
  goal_2 by  
    grit1_2* grit3_2* grit5_2* grit6_2* (L5-L8);
  
  !estimating thresholds freely

  [grit1_2$1-grit8_2$1*] (T1_1-T1_8);
  [grit1_2$2-grit8_2$2*] (T2_1-T2_8);
  [grit1_2$3-grit8_2$3*] (T3_1-T3_8);
  [grit1_2$4-grit8_2$4*] (T4_1-T4_8);

  !constraining item residual variance to 1 for identification
  grit1_2-grit8_2@1;

  !fixing latent mean to zero for identification
  [pers_2@0];
  [goal_2@0];
  
  !fixing latent variance to one for identification
  pers_2@1;
  goal_2@1;
  
!!! Model for Older Students !!!

Model Older:
  ! Factor loadings constrained to *be equal to the control group*
  pers_2 by
    grit2_2* grit4_2* grit7_2* grit8_2* (L1-L4);
    
  goal_2 by  
    grit1_2* grit3_2* grit5_2* grit6_2* (L5-L8);

  ! *thresholds held equal to the control group when left blank*
  
  ! constraining item residual variance to 1 for identification
  grit1_2-grit8_2@1;

  ! latent factor mean *estimated*
  [pers_2*];
  [goal_2*];
  
  ! latent factor variance *estimated*
  pers_2*;
  goal_2*;
     
Output: sampstat stdyx modindices(all);
Savedata: Difftest is grit2_student_age_inv_scalar.dat;
