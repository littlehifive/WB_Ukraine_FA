Title: Student Sleep Age Measurement Invariance Metric Model;

Data:
   File is ../dat_stu_1.txt;

Variable:
   Names are studentid index treatment girl age_w child_grade in_ukr
   guardian_living guardian_region 
   grit1_1 grit2_1 grit3_1 grit4_1 grit5_1 grit6_1 grit7_1 grit8_1 
   loc1_1 loc2_1 loc3_1 loc4_1 
   dass1_1 dass2_1 dass3_1 dass4_1 dass5_1 dass6_1 dass7_1 dass8_1 
   dass9_1 dass10_1 dass11_1 dass12_1 dass13_1 dass14_1 
   sleep1_1 sleep2_1 sleep3_1 sleep4_1 sleep5_1 sleep6_1 sleep7_1 
   edugoals_1 half; 
   Missing are . ;

  Usevariable are
	sleep1_1 sleep2_1 sleep3_1 sleep4_1 sleep5_1 sleep6_1 sleep7_1 age_c;
   
  Categorical is
	sleep1_1 sleep2_1 sleep3_1 sleep4_1 sleep5_1 sleep6_1 sleep7_1;
    
  Idvariable is studentid;  
    
  Group is age_c (1 = Older 0 = Younger);

Define: 
	age_c = 0;
	if(age_w ge 13) then age_c = 1;
	
Analysis:
   Estimator = wlsmv;
   Parameterization = theta;
   Difftest = sleep1_student_age_inv_config.dat
  
  
!!! Model for Younger Students !!!

Model:
  ! Factor loadings all freely estimated
  sleep_1 by
    sleep1_1-sleep7_1* (L1-L7);
    
  sleep1_1 with sleep2_1;
  sleep6_1 with sleep7_1;
  
  !estimating thresholds freely
  [sleep1_1$1-sleep7_1$1*] (T1_1-T1_7);
  [sleep1_1$2-sleep7_1$2*] (T2_1-T2_7);
  [sleep1_1$3-sleep7_1$3*] (T3_1-T3_7);
  
  !constraining item residual variance to 1 for identification
  sleep1_1-sleep7_1@1;

  !fixing latent mean to zero for identification
  [sleep_1@0];
  
  !fixing latent variance to one for identification
  sleep_1@1;
  
!!! Model for Older Students !!!

Model Older:
  ! Factor loadings constrained to *be equal to the control group*
  sleep_1 by sleep1_1-sleep7_1* (L1-L7);
    
  sleep1_1 with sleep2_1;
  sleep6_1 with sleep7_1;
  
  ! estimating thresholds freely
  [sleep1_1$1-sleep7_1$1*];
  [sleep1_1$2-sleep7_1$2*];
  [sleep1_1$3-sleep7_1$3*];
  
  ! constraining item residual variance to 1 for identification
  sleep1_1-sleep7_1@1;

  ! fixing latent factor mean to zero for identification
  [sleep_1@0];
  
  ! latent factor variance *estimated*
  sleep_1*;
     
Output: sampstat stdyx modindices(all);
Savedata: Difftest is sleep1_student_age_inv_metric.dat;
